?expr : case

?case : logop
      | "case"i caseleg+ ("else"i logop)? "end"i

caseleg : "when"i logop "then"i logop

?logop : relop
       | relop LOGOPERATOR logop

?relop : sum
       | sum RELOPERATOR relop
       | sum "between"i sum "and"i sum -> between
       | sum _NOT "between"i sum "and"i sum -> notbetween
       | sum _IN "(" expr ("," expr)* ")" -> instmt
       | sum _NOT _IN "(" expr ("," expr)* ")" -> notinstmt

?sum : product
    | sum "+" product -> add
    | sum "-" product -> sub

?product : boolnot
         | product "*" boolnot -> mul
         | product "/" boolnot -> div
         | product "%" boolnot -> mod


?boolnot : _NOT atom -> bnot
         | "-" atom -> negate
         | atom

?atom : SQ_STR -> lit_str
      | SIGNED_INT -> lit_int
      | _NVL expr ("," expr)* ")"  -> nvl
      | col_name
      | _TRUE -> true
      | _FALSE -> false
      | _NULL -> null
      | "(" expr ")"

// Need priority to compete with CNAME
_TRUE.10 : "true"i
_FALSE.10 : "false"i
_NULL.10  : "null"i
_NOT.10 : "not"i
_NVL.10 : "nvl("i
_IN.10 : "in"i

RELOPERATOR : "="|"<"|">"|"<="|">="|"!="
LOGOPERATOR : "and" | "or"

// CNAME is pretty broad, so needs to be low
col_name : CNAME | DQ_STR

%import common.CNAME
%import common.ESCAPED_STRING -> DQ_STR
%import common._STRING_ESC_INNER
%import common.SIGNED_INT

SQ_STR : "'" _STRING_ESC_INNER "'"

%import common.WS
%ignore WS