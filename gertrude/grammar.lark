?expr : logop

?logop : relop
       | relop LOGOPERATOR logop

?relop : sum
       | sum RELOPERATOR relop

?sum : product
    | sum "+" product -> add
    | sum "-" product -> sub

?product : atom
         | product "*" atom -> mul
         | product "/" atom -> div

?atom : SQ_STR -> lit_str
      | SIGNED_INT -> lit_int
      | col_name
      | /true/i -> true
      | /false/i -> false
      | /null/i -> null
      | "(" sum ")"


RELOPERATOR : "="|"<"|">"|"<="|">="|"!="
LOGOPERATOR : "and" | "or"

# CNAME is pretty broad, so needs to be low
col_name : CNAME | DQ_STR

%import common.CNAME
%import common.ESCAPED_STRING -> DQ_STR
%import common._STRING_ESC_INNER
%import common.SIGNED_INT

SQ_STR : "'" _STRING_ESC_INNER "'"

%import common.WS
%ignore WS