?expr : case

?case : logop
      | "case"i caseleg+ ("else"i logop)? "end"i

caseleg : "when"i logop "then"i logop

?logop : relop
       | relop LOGOPERATOR logop

?relop : sum
       | sum RELOPERATOR relop
       | sum "between"i sum "and"i sum -> between
       | sum _NOT "between"i sum "and"i sum -> notbetween
       | sum _IN "(" expr ("," expr)* ")" -> instmt
       | sum _NOT _IN "(" expr ("," expr)* ")" -> notinstmt

?sum : product
    | sum "+" product -> add
    | sum "-" product -> sub

?product : boolnot
         | product "*" boolnot -> mul
         | product "/" boolnot -> div
         | product "%" boolnot -> mod


?boolnot : _NOT negate -> bnot
         | negate "is" _NULL -> isnull
         | negate "is" _NOT _NULL -> isnotnull
         | negate

?negate : "-" negate -> negative
        | atom

?atom : SQ_STR -> lit_str
      | SIGNED_INT -> lit_int
      | SIGNED_FLOAT -> lit_float
      | _NVL expr ("," expr)* ")"  -> nvl
      | _SUBSTR expr "," expr ("," expr)? ")" -> substring
      | _STRLEN expr ")" -> strlen
      | _UPPER expr ")" -> upper
      | _LOWER expr ")" -> lower
      | _TO_STR expr ")" -> tostr
      | _TO_INT expr ")" -> toint
      | col_name
      | _TRUE -> true
      | _FALSE -> false
      | _NULL -> null
      | "(" expr ")"

// Need priority to compete with CNAME
_TRUE.10 : "true"i
_FALSE.10 : "false"i
_NULL.10  : "null"i
_NOT.10 : "not"i
_IN.10 : "in"i

// functions
_NVL.10 : "nvl("i
_SUBSTR.10 : "substr("i
_STRLEN.10 : "strlen("i
_UPPER.10 : "upper("i
_LOWER.10 : "lower("i
_TO_STR.10 : "str("i
_TO_INT.10 : "int("i

RELOPERATOR : "="|"<"|">"|"<="|">="|"!="
LOGOPERATOR : "and" | "or"

// CNAME is pretty broad, so needs to be low
col_name : CNAME | DQ_STR

%import common.CNAME
%import common.ESCAPED_STRING -> DQ_STR
%import common._STRING_ESC_INNER
%import common.SIGNED_INT
%import common.SIGNED_FLOAT

SQ_STR : "'" _STRING_ESC_INNER "'"

%import common.WS
%ignore WS